#!./sed -f
# sed calculator (test + benchmark for sed)
# usage: ./infinadd <<< '(9 + 3) / 2'

s/ //g
s/[0-9]/<&/g
s/0//g; s/1/|/g; s/2/||/g; s/3/|||/g; s/4/||||/g; s/5/|||||/g; s/6/||||||/g; s/7/|||||||/g; s/8/||||||||/g; s/9/|||||||||/g
: tens; s/|</<||||||||||/g; t tens
s/<//g
s/+//g
: start
: minus; s/|-|/-/g; t minus; s/-$//
s/\*/*</
: multiply; s |\(|\*|*\)<\(|*\) \1\2<\2 ; t multiply;
s/|\*//; s/<//
s /\(|*\) /<\1> ; s %\(|*\) %<\1> ;
/<>/{ cerror: division by zero
  q1; }
: divide
s /<\(|\+\)> /\1<\1> ; s %<\(|\+\)> %\1<\1> ;
: sub; s |/| / ; s |%| % ; t sub
/\/</s > >| ; /%</s > >| ;
/||*\//b divide; /||*%/b divide;
s |*/|*<|*>  ; s |*%\(|*\)<|*>|* \1 ;
s/(\(|*\))/\1/g
t start
: back
s/||||||||||/</g
s/<\([0-9]*\)$/<0\1/
s/|||||||||/9/; s/||||||||/8/; s/|||||||/7/; s/||||||/6/; s/|||||/5/; s/||||/4/; s/|||/3/; s/||/2/; s/|/1/
s/</|/g
t back
s/^$/0/
