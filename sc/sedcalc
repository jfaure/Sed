#!./sed -rf
# sed calculator (test + benchmark for sed)
# usage: ./sedcalc <<< '3 + (9 - 11) / 2'

: welcome; s/[^0-9*/%()+-]//g; s/[0-9]/<&/g;
s/0//g; s/1/|/g; s/2/||/g; s/3/|||/g; s/4/||||/g; s/5/|||||/g; s/6/||||||/g; s/7/|||||||/g; s/8/||||||||/g; s/9/|||||||||/g
:10; s/|</<||||||||||/g; t10; s/<//g; s/|(/|*(/g; s/)|/)*|/g
: ^; s/(\(-*|*\))/\1/g; t ^; : ready? #l
s/%[+-]\+/%/g; s/--/+/g; s/+++*/+/g; s/-+\|+-/-/g;
s/\(^\|[^|)]\)+/\1/; s/[+-]\($\|[^|(]\)/\1/; t ^;
s/\(^\|(\)[%/*]\(||*\)/\1/g; s/\(|*\)\*\($\|)\)/\2/g;
s/\(^\|[-(+*/]\)\(||*[/%*]\)-\(||*\)/\1-\2\3/g; t ^;
: *; s/\(\(^\|(\)[^%/]*|\)\*|/\1*<|/;
  : while*; s |\(|\*|*\)<\(|*\) \1\2<\2 ; t while*;
  s/|\*\(|*\)</\1/; t ^;
: %/; /[%/]\($\|)\)/{s/.*\([%/]\).*/error: \10/; q1}
  /\(^\|(\)[^%]*\//s |/\(||*\) |/<\1> ;
  /</!s |%\(||*\) |%<\1> ;
  : %/loop; s /<\(|\+\)> /\1<\1> ; s %<\(|\+\)> %\1<\1> ;
  : %/sub; s \(||*\)\([/%]\)\1< \2< ; t %/sub;
  /[/%]</s > >| ; t %/loop; s |*/|*<|*>  ; s/-\([^|(]\)/\1/;
  /%/{s/>|*//; s/%<|*//; s/\(|*\)%\(|*\)<\(|*\)/(\1\3-\2)/};
  t ^;
: -;
  s/-\(||*\)-\(||*\)\($\|[)+-]\)/-\1\2\3/;
  s/-\(||*\)+\(||*\)\($\|[)+-]\)/\2-\1\3/;
  t ^; /([+|]*)/b +; s/\(||*\)-\1/-/; s/-\([^|(]\)/\1/; 
  t ^;
: +; s/|+\(|*\)\($\|)\)/|\1\2/; t ^;
/[()]/s/^/mismatched parenthesis: /
: $; s/|\{10\}/</g; s/<\([0-9]*\)$/<0\1/
s/|\{9\}/9/; s/|\{8\}/8/; s/|\{7\}/7/; s/|\{6\}/6/;
s/|||||/5/; s/||||/4/; s/|||/3/; s/||/2/; s/|/1/
s/</|/g; t $ 
s/^$/0/;
