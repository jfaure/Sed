#!./sed -f
# sed calculator (test + benchmark for sed)
# usage: ./sedcalc <<< '3 + (9 - 11) / 2'

s/ //g; s/[^0-9*/%()+-]//g
s/[0-9]/<&/g
s/0//g; s/1/|/g; s/2/||/g; s/3/|||/g; s/4/||||/g; s/5/|||||/g; s/6/||||||/g; s/7/|||||||/g; s/8/||||||||/g; s/9/|||||||||/g
: tens; s/|</<||||||||||/g; t tens
s/<//g
: start
s/\*/*</g
: multiply; s |\(|\*|*\)<\(|*\) \1\2<\2 g; t multiply;
s/|\*//g; s/<//g
s/%[+-]*/%/g
/\/[^|]/b add; /[^|]\// b add; /[^|]%/ b add; /%[^|]/b add;
/\/(/b add; /)\// b add; /)%/b add; /%(/b add;
s /\(|*\) /<\1> ; s %\(|*\) %<\1> ;
/<>/{ cerror: division by zero
  q1; }
: divide; s /<\(|\+\)> /\1<\1> ; s %<\(|\+\)> %\1<\1> ;
: divsub; s |/| / ; s |%| % ; t divsub
/\/</s > >| ; /%</s > >| ;
/||*\//b divide; /||*%/b divide;
s |*/|*<|*>  ;
/%/{s/>|*//g; /%</s/%<|*//g; s/\(|*\)<\(|*\)/\2-\1/g; s/%//g;}
: add; s/+//g; s/--//g; s/-\(|*\)\(.\)-\(|*\)/\1\2\3/
s/\(||*[^|]\)-\(||*\)/-\1\2/g; t start;
: minus; s/|-|/-/g; t minus; s/-$//
: paren; s/(\(|*\))/\1/g; s/(\(-|*\))/\1/g; t start
: back; s/|\{10\}/</g
s/<\([0-9]*\)$/<0\1/
s/|\{9\}/9/; s/|\{8\}/8/; s/|\{7\}/7/; s/|\{6\}/6/;
s/|||||/5/; s/||||/4/; s/|||/3/; s/||/2/; s/|/1/
s/</|/g
t back
s/^$/0/
