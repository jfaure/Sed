#!./sed -f
# sed calculator (test + benchmark for sed)
# usage: ./sedcalc <<< '3 + (9 - 11) / 2'

s/[^0-9*/%()+-]//g; s/[0-9]/<&/g;
s/0//g; s/1/|/g; s/2/||/g; s/3/|||/g; s/4/||||/g; s/5/|||||/g; s/6/||||||/g; s/7/|||||||/g; s/8/||||||||/g; s/9/|||||||||/g
:10; s/|</<||||||||||/g; t10; s/<//g; s/|(/|*(/g; s/)|/)*|/g
: start; s/(\(-*|*\))/\1/g; t start; :clean:
s/%[+-]\+/%/g; s/--/+/g; s/+++*/+/g; s/-+\|+-/-/g;
s/\(^\|[^|)]\)+/\1/; s/[+-]\($\|[^|(]\)/\1/; t start;
s/\(^\|(\)[%/*]\(|*\)/\1/g; s/\(|*\)\*\($\|)\)/\2/g;
s/\(||*[/%*]\)-\(||*\)/-\1\2/g;
t start;
: *; s/|\*|/|*<|/;
  : while*; s |\(|\*|*\)<\(|*\) \1\2<\2 ; t while*;
  s/|\*//; s/<//; t start;
: %/; /[%/]\($\|)\)/{s/.*\([%/]\).*/error: \10/; q1}
s |/\(||*\) |/<\1> ; s |%\(||*\) |%<\1> ;
  : %/loop; s /<\(|\+\)> /\1<\1> ; s %<\(|\+\)> %\1<\1> ;
  : %/sub; s \(||*\)\([/%]\)\1< \2< ; t %/sub
  /[/%]</s > >| ; t %/loop; s /|*<|*>  ;
  /%/{s/>|*//; s/%<|*//; s/%\(|*\)<\(|*\)/\2-\1/}; t start;
: +-; s/-\(|*\)+\(|*\)/\2-\1/
  : add; s/|+|/||/g; s/-\(||*\)-\(||*\)/-(\1+\2)/
  s/\(||*\)-\1/-/g; s/-$//
t start;
/[()]/{s/^/mismatched parenthesis/; q1}
: $; s/|\{10\}/</g; s/<\([0-9]*\)$/<0\1/
s/|\{9\}/9/; s/|\{8\}/8/; s/|\{7\}/7/; s/|\{6\}/6/;
s/|||||/5/; s/||||/4/; s/|||/3/; s/||/2/; s/|/1/
s/</|/g; t $ 
s/^$/0/;
