#!./sed -f
# sed calculator (test + benchmark for sed)
# usage: ./sedcalc <<< '3 + (9 - 11) / 2'

s/[^0-9*/%()+-]//g; s/[0-9]/<&/g;
s/0//g; s/1/|/g; s/2/||/g; s/3/|||/g; s/4/||||/g; s/5/|||||/g; s/6/||||||/g; s/7/|||||||/g; s/8/||||||||/g; s/9/|||||||||/g
: tens; s/|</<||||||||||/g; t tens; s/<//g;
: start; s/%[+-]\+/%/g; s/\([-*%/]\)+/\1/g;
s/--/+/g; s/+-/-/g; t start;
s/\(||*[/%*]\)-\(||*\)/-\1\2/g;
s/|\*|/|*<|/;
: multiply; s |\(|\*|*\)<\(|*\) \1\2<\2 ; t multiply;
s/|\*//; s/<//; t start;
s |/\(||*\) |/<\1> ; s |%\(||*\) |%<\1> ;
/<>/{ s/.*\([%/]\)|*<>.*/\10 is undefined/; q1; }
: divide; s /<\(|\+\)> /\1<\1> ; s %<\(|\+\)> %\1<\1> ;
: divsub; s \(||*\)\([/%]\)\1 \2 ; t divsub
/[/%]</s > >| g; t divide; s /|*<|*>  ; t start;
/%/{s/>|*//g; s/%<|*//g; s/%\(|*\)<\(|*\)/\2-\1/g}
s/-\(|*\)+\(|*\)/\2-\1/g
: add; s/+//g; s/--//g; s/-\(||*\)-\(||*\)/-(\1+\2)/
t start;
s/\(||*\)-\1/-/g; s/-$//g
: paren; s/|(/|*(/g; s/)|/)*|/g; s/(\(-*|*\))/\1/g; t start
/[()]/{s/.*/mismatched parenthesis/; q1}
: back; s/|\{10\}/</g; s/<\([0-9]*\)$/<0\1/
s/|\{9\}/9/; s/|\{8\}/8/; s/|\{7\}/7/; s/|\{6\}/6/;
s/|||||/5/; s/||||/4/; s/|||/3/; s/||/2/; s/|/1/
s/</|/g; t back
s/^$/0/
